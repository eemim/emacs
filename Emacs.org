#+TITLE: Emacs Configuration
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el :mkdirp yes

* Table of Contents :toc:
- [[#startup-and-basic-settings][Startup and Basic Settings]]
  - [[#backup--autosave-management][Backup & Autosave management]]
  - [[#system-binaries][System binaries]]
- [[#startup-performance][Startup performance]]
- [[#ui-configuration][UI Configuration]]
  - [[#font-configuration][Font Configuration]]
  - [[#line-numbers][Line Numbers]]
- [[#package-management][Package Management]]
  - [[#automatic-package-update][Automatic package update]]
- [[#editing-configuration][Editing Configuration]]
- [[#completion-and-navigation][Completion and Navigation]]
  - [[#ivy][Ivy]]
  - [[#counsel][Counsel]]
  - [[#which-key][Which Key]]
  - [[#helpful][Helpful]]
- [[#appearance][Appearance]]
  - [[#icons][Icons]]
  - [[#modeline][Modeline]]
  - [[#theme][Theme]]
  - [[#rainbow-delimiters][Rainbow Delimiters]]
- [[#keybindings-and-evil-mode][Keybindings and Evil Mode]]
  - [[#evil-mode][Evil Mode]]
  - [[#general-leader-keys][General (Leader Keys)]]
  - [[#hydra][Hydra]]
- [[#org-mode-configuration][Org Mode Configuration]]
  - [[#org-mode-setup-functions][Org Mode Setup Functions]]
  - [[#org-mode-package][Org Mode Package]]
  - [[#org-toc][Org Toc]]
  - [[#org-bullets][Org Bullets]]
  - [[#visual-fill-column][Visual Fill Column]]
- [[#org-babel-setup][Org Babel Setup]]
  - [[#babel-languages][Babel Languages]]
  - [[#org-tempo-templates][Org Tempo Templates]]
  - [[#auto-tangle-configuration][Auto-tangle Configuration]]
- [[#development][Development]]
  - [[#version-control][Version Control]]
  - [[#language-server-eglot][Language Server (Eglot)]]
  - [[#breadcrumbs][Breadcrumbs]]
  - [[#treemacs][Treemacs]]
  - [[#direnv][Direnv]]
  - [[#completion-company][Completion (Company)]]
  - [[#commenting][Commenting]]
  - [[#file-management][File management]]
  - [[#bracket-completion][Bracket completion]]
- [[#terminal][Terminal]]
  - [[#vterm][VTerm]]
- [[#runtime-performance][Runtime performance]]

* Startup and Basic Settings

#+begin_src emacs-lisp
  (setq inhibit-startup-message t)
  (setq native-comp-async-report-warnings-errors nil)


  ;; Move customization variables to a separate file and load it
  (setq custom-file (locate-user-emacs-file "custom-vars.el"))
  (load custom-file 'noerror 'nomessage)
#+end_src

** Backup & Autosave management

#+begin_src emacs-lisp
  ;; Backups (~ files)
  (setq backup-directory-alist
        '(("." . "~/.emacs.d.backup/file-backups/"))
        backup-by-copying t
        version-control t
        delete-old-versions t
        kept-new-versions 6
        kept-old-versions 2)

  ;; Auto-saves (# files)
  (setq auto-save-file-name-transforms
        '((".*" "~/.emacs.d.backup/auto-saves/" t)))
#+end_src

** System binaries

#+begin_src emacs-lisp

  (use-package exec-path-from-shell
    :if (memq window-system '(x pgtk wayland))
    :config
    (exec-path-from-shell-initialize))

#+end_src

* Startup performance

#+begin_src emacs-lisp
  ;; The default is 800 kilobytes.  Measured in bytes.
  (setq gc-cons-threshold (* 50 1000 1000))
  
  (defun my/display-startup-time ()
    (message "Emacs loaded in %s with %d garbage collections."
             (format "%.2f seconds"
                     (float-time
                       (time-subtract after-init-time before-init-time)))
             gcs-done))

  (add-hook 'emacs-startup-hook #'my/display-startup-time)
#+end_src

* UI Configuration

#+begin_src emacs-lisp

  (scroll-bar-mode -1)        ; Disable visible scrollbar
  (tool-bar-mode -1)          ; Disable the toolbar
  (tooltip-mode -1)           ; Disable tooltips
  (set-fringe-mode 10)        ; Give some breathing room
  (menu-bar-mode -1)          ; Disable the menu bar

  ;; Set up the visible bell
  (setq visible-bell t)

  ;; Full-screen
  (add-to-list 'default-frame-alist '(fullscreen . fullboth))

#+end_src

** Font Configuration

#+begin_src emacs-lisp

  (set-face-attribute 'default nil :font "JetBrains Mono" :height 120)

#+end_src

** Line Numbers

#+begin_src emacs-lisp

  ;; Line numbers
  (column-number-mode)
  (global-display-line-numbers-mode t)

  (dolist (mode '(org-mode-hook
                  term-mode-hook
                  vterm-mode-hook
                  treemacs-mode-hook
                  shell-mode-hook
                  eshell-mode-hook))
    (add-hook mode (lambda() (display-line-numbers-mode 0))))

#+end_src

* Package Management

#+begin_src emacs-lisp

  ;; Initialize package sources
  (require 'package)

  (setq package-archives '(("melpa" . "https://melpa.org/packages/")
                           ("org" . "https://orgmode.org/elpa/")
                           ("elpa" . "https://elpa.gnu.org/packages/")))

  (package-initialize)
  (unless package-archive-contents
   (package-refresh-contents))

  ;; Initialize use-package on non-Linux platforms
  (unless (package-installed-p 'use-package)
     (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure t)

#+end_src

** Automatic package update

#+begin_src emacs-lisp

  (use-package auto-package-update
    :custom
    (auto-package-update-interval 7)
    (auto-package-update-prompt-before-update t)
    (auto-package-update-hide-results t)
    :config
    (auto-package-update-maybe)
    (auto-package-update-at-time "09:00"))

#+end_src

* Editing Configuration

#+begin_src emacs-lisp

  ;; Make ESC quit prompts
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

  ;; Enable soft wrapping globally (works with evil-respect-visual-line-mode)
  (global-visual-line-mode 1)

#+end_src

* Completion and Navigation

** Ivy

#+begin_src emacs-lisp

    (use-package ivy
      :diminish
      :init
      (ivy-mode 1)
      :bind (("C-s" . swiper)
             :map ivy-minibuffer-map
             ("TAB" . ivy-alt-done)	
             ("C-l" . ivy-alt-done)
             ("C-j" . ivy-next-line)
             ("C-k" . ivy-previous-line)
             :map ivy-switch-buffer-map
             ("C-k" . ivy-previous-line)
             ("C-l" . ivy-done)
             ("C-d" . ivy-switch-buffer-kill)
             :map ivy-reverse-i-search-map
             ("C-k" . ivy-previous-line)
             ("C-d" . ivy-reverse-i-search-kill))
       :config (setq ivy-re-builders-alist
               '((counsel-M-x . ivy--regex-ignore-order)
                (t . ivy--regex-plus))))

    (use-package ivy-rich
      :after ivy
      :init
      (ivy-rich-mode 1))

#+end_src

** Counsel

#+begin_src emacs-lisp

  (use-package counsel
    :after ivy
    :bind (("M-x" . counsel-M-x)
           ("C-x C-f" . counsel-find-file)
           ("C-x b" . counsel-switch-buffer)
           :map minibuffer-local-map
           ("C-r" . 'counsel-minibuffer-history)))

#+end_src

** Which Key

#+begin_src emacs-lisp

  (use-package which-key
       :defer 0 
       :diminish which-key-mode
       :config
       (which-key-mode)
       (setq which-key-idle-delay 0.3))

#+end_src

** Helpful

#+begin_src emacs-lisp

  (use-package helpful
    :after counsel
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command]  . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key]      . helpful-key))

#+end_src

* Appearance

** Icons

#+begin_src emacs-lisp

  (use-package all-the-icons)

#+end_src

** Modeline

#+begin_src emacs-lisp

  (use-package doom-modeline
    :init
    (setq doom-modeline-icon t
          doom-modeline-height 40)
    :config
    (doom-modeline-mode 1))

#+end_src

** Theme

#+begin_src emacs-lisp

  (use-package doom-themes
    :init
    (setq doom-themes-enable-bold t
          doom-themes-enable-italic t)
    :config
    (load-theme 'doom-material-dark t))

#+end_src

** Rainbow Delimiters

#+begin_src emacs-lisp

  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))

#+end_src

* Keybindings and Evil Mode

** Evil Mode

#+begin_src emacs-lisp

  (use-package evil
    :init
    (setq evil-want-integration t
          evil-want-keybinding nil
          evil-want-C-u-scroll t
          evil-undo-system 'undo-redo
          evil-respect-visual-line-mode t)
    :config
    (evil-mode 1)

    ;; Exit insert mode with C-g
    (define-key evil-insert-state-map (kbd "C-g") #'evil-normal-state)

    ;; Buffers that should start in normal mode
    (evil-set-initial-state 'messages-buffer-mode 'normal))

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))

#+end_src

** General (Leader Keys)

#+begin_src emacs-lisp

  (use-package evil-nerd-commenter)

  (use-package general
      :config
      ;; Make General work with Evil
      (general-evil-setup t)

      ;; Define leader key
      (general-create-definer my/leader-keys
      :states '(normal visual motion emacs)
      :keymaps 'override
      :prefix "SPC")

      ;; Leader bindings
      (my/leader-keys

      ;; Files
      "f"  '(:ignore t :which-key "files")
      "ff" '(counsel-find-file :which-key "find file")
      "fb" '(counsel-switch-buffer :which-key "find buffer")
      "fde" '(lambda () (interactive) (find-file (expand-file-name "~/.config/emacs/Emacs.org")))

      ;; Buffer management
      "b"  '(:ignore t :which-key "buffers")
      "br" '(rename-buffer :which-key "rename buffer")
      "bw" '(save-buffer :which-key "save file")
      "bx" '(kill-current-buffer :which-key "kill buffer")
      "bn" '(switch-to-next-buffer :which-key "next buffer")
      "bp" '(switch-to-prev-buffer :which-key "previous buffer")

      ;; Toggles
      "t"  '(:ignore t :which-key "toggles")
      "tt" '(counsel-load-theme :which-key "choose theme")

      ;; Terminal
      "o"  '(:ignore t :which-key "open")
      ;"ot" '(vterm :which-key "vterm terminal")
      "ot" '(multi-vterm :which-key "new vterm")
      "op" '(multi-vterm-project :which-key "vterm in project")

      ;; Eglot (LSP)
      "l"  '(:ignore t :which-key "lsp")
      "la" '(eglot-code-actions :which-key "code actions")
      "lr" '(eglot-rename :which-key "rename")
      "lf" '(eglot-format :which-key "format")
      "ld" '(xref-find-definitions :which-key "go to definition")
      "lD" '(xref-find-references :which-key "find references")

      ;; Dired
      "e" '(dired-jump :which key "explorer")

      ;; SmartBrackets
      "s" '(:ignore t :which-key "wrap")
      "sr" '(sp-wrap-round   :which-key "round ()")
      "ss" '(sp-wrap-square  :which-key "square []")
      "sc" '(sp-wrap-curly   :which-key "curly {}")
      "s\"" '(sp-wrap-double-quote :which-key "double quotes \"\"")
      "s'" '(sp-wrap-single-quote :which-key "single quotes ''")
      "s`" '(sp-wrap-backtick :which-key "backticks ``")))

#+end_src

** Hydra

#+begin_src emacs-lisp

  (use-package hydra
    :defer t)

  (defhydra hydra-text-scale (:timeout 4)
    "scale text"
    ("j" text-scale-increase "in")
    ("k" text-scale-decrease "out")
    ("f" nil "finished" :exit t))

  (my/leader-keys
    "ts" '(hydra-text-scale/body :which-key "scale text"))

#+end_src

* Org Mode Configuration

** Org Mode Setup Functions

#+begin_src emacs-lisp

  (defun my/org-mode-setup ()
    (org-indent-mode)
    (visual-line-mode 1))

  (defun my/org-font-setup ()
    ;; Replace list hyphen with dot
    (font-lock-add-keywords 'org-mode
                            '(("^ *\\([-]\\) "
                               (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•")))))))

  (defun my/org-font-scale ()
    ;; Slightly larger body text (Org buffers only)
    (face-remap-add-relative 'default :height 1.15)

    ;; Headings hierarchy
    (dolist (face '((org-level-1 . 1.3)
                    (org-level-2 . 1.25)
                    (org-level-3 . 1.2)
                    (org-level-4 . 1.15)
                    (org-level-5 . 1.1)
                    (org-level-6 . 1.05)))
      (set-face-attribute (car face) nil :height (cdr face)))

    ;; Document title
    (set-face-attribute 'org-document-title nil :height 1.4 :weight 'bold))

#+end_src

** Org Mode Package

#+begin_src emacs-lisp

  (use-package org
    :hook ((org-mode . my/org-mode-setup)
           (org-mode . my/org-font-scale))
    :config
    (setq org-ellipsis " ▾"
            org-hide-emphasis-markers t
            org-edit-src-content-indentation 2  ; Indentation inside source blocks
            org-src-preserve-indentation nil)   ; Let org-mode manage indentation
    (my/org-font-setup))

#+end_src

** Org Toc

#+begin_src emacs-lisp

  (use-package toc-org
    :hook (org-mode . toc-org-mode))

#+end_src

** Org Bullets

#+begin_src emacs-lisp

  (use-package org-bullets
    :after org
    :hook (org-mode . org-bullets-mode)
    :custom
    (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●")))

#+end_src

** Visual Fill Column

#+begin_src emacs-lisp

  (defun my/org-mode-visual-fill ()
    (setq visual-fill-column-width 100
          visual-fill-column-center-text t)
    (visual-fill-column-mode 1))

  (use-package visual-fill-column
    :hook (org-mode . my/org-mode-visual-fill))

#+end_src

* Org Babel Setup

** Babel Languages

#+begin_src emacs-lisp

  (with-eval-after-load 'org
    (org-babel-do-load-languages
        'org-babel-load-languages
        '((emacs-lisp . t)
        (python . t)))

    (push '("conf-unix" . conf-unix) org-src-lang-modes))

#+end_src

** Org Tempo Templates

#+begin_src emacs-lisp

  (with-eval-after-load 'org
    ;; This is needed as of Org 9.2
    (require 'org-tempo)

    (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
    (add-to-list 'org-structure-template-alist '("py" . "src python")))

#+end_src

** Auto-tangle Configuration

#+begin_src emacs-lisp

  ;; Automatically tangle our Emacs.org config file when we save it
  (defun my/org-babel-tangle-config ()
    (when (string-equal (buffer-file-name)
                        (expand-file-name "~/.config/emacs/Emacs.org"))
      ;; Dynamic scoping to the rescue
      (let ((org-confirm-babel-evaluate nil))
        (org-babel-tangle))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'my/org-babel-tangle-config)))

#+end_src

* Development

** Version Control

#+begin_src emacs-lisp

  (use-package magit
    :commands magit-status 
    :custom
   (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))

#+end_src

** Language Server (Eglot)

#+begin_src emacs-lisp

  (use-package eglot
    :ensure nil  ; Built-in from Emacs 29+
    :commands (eglot eglot-ensure)
    :config
    (setq eglot-report-progress nil)  ; Disable progress reporting in modeline

    ;; Shutdown server when last buffer is closed
    (setq eglot-autoshutdown t))

#+end_src

*** Python

#+begin_src emacs-lisp

  (use-package python-mode
    :ensure nil
    :hook (python-mode . eglot-ensure)
    :config
    (setq python-indent-offset 4))

  ;; Python formatting with black
  (use-package python-black
    :demand t
    :after python
    :custom
    (python-black-extra-args '("--target-version" "py312")))

#+end_src

*** TypeScript

#+begin_src emacs-lisp

  (use-package typescript-mode
    :mode "\\.ts\\'"
    :hook (typescript-mode . eglot-ensure)
    :config
    (setq typescript-indent-level 2))

#+end_src


** Breadcrumbs

#+begin_src emacs-lisp

(use-package breadcrumb
  :hook (prog-mode . breadcrumb-mode))
  
#+end_src

** Treemacs

#+begin_src emacs-lisp

  (use-package treemacs
    :ensure t
    :defer t
    :config
    ;; Follow current file
    (treemacs-follow-mode t)

    ;; Automatically switch projects
    (treemacs-project-follow-mode t)

    ;; Clean UI defaults
    (setq treemacs-is-never-other-window t
          treemacs-width 30))

  (use-package treemacs-evil
    :after (treemacs evil)
    :ensure t)
  
#+end_src

** Direnv

- To use the .venv automatically, first you must create *.envrc* with
  =echo 'source .venv/bin/activate' > .envrc=

- Next you have to enable *direnv* with
  =direnv allow=

#+begin_src emacs-lisp

  (use-package direnv
    :config
    (direnv-mode))

#+end_src

** Completion (Company)

#+begin_src emacs-lisp

  (use-package company
    :hook (prog-mode . company-mode)
    :bind (:map company-active-map
           ("<tab>" . company-complete-selection)
           ("C-n" . company-select-next)
           ("C-p" . company-select-previous))
    :custom
    (company-minimum-prefix-length 1)
    (company-idle-delay 0.1))  ; Slightly higher for better performance

  (use-package company-box
    :hook (company-mode . company-box-mode))

#+end_src

** Commenting

#+begin_src emacs-lisp

  (use-package evil-nerd-commenter
    :bind ("M-/" . evilnc-comment-or-uncomment-lines))

#+end_src

** File management

*** Dired

#+begin_src emacs-lisp

  (use-package dired
    :ensure nil
    :commands (dired dired-jump)
    :custom
    (dired-listing-switches "-agho --group-directories-first")
    (dired-kill-when-opening-new-dired-buffer t)
    :config
    (setq dired-dwim-target t)
    (evil-collection-define-key 'normal 'dired-mode-map
      "h" #'dired-up-directory
      "l" #'dired-find-file))

  (use-package all-the-icons-dired
    :hook (dired-mode . all-the-icons-dired-mode)
    :custom
    (all-the-icons-dired-monochrome nil))

  (use-package dired-hide-dotfiles
    :hook (dired-mode . dired-hide-dotfiles-mode)
    :config
    (evil-collection-define-key 'normal 'dired-mode-map
        "H" 'dired-hide-dotfiles-mode))

#+end_src

** Bracket completion

#+begin_src emacs-lisp

  (use-package smartparens
    :ensure t
    :hook (prog-mode . smartparens-mode)
    :config
    (require 'smartparens-config)
    (show-smartparens-mode 1))

  ;; Helper functions for quotes
  (defun sp-wrap-double-quote ()
    "Wrap region or symbol in double quotes."
    (interactive)
    (sp-wrap-with-pair "\""))

  (defun sp-wrap-single-quote ()
    "Wrap region or symbol in single quotes."
    (interactive)
    (sp-wrap-with-pair "'"))

  (defun sp-wrap-backtick ()
    "Wrap region or symbol in backticks."
    (interactive)
    (sp-wrap-with-pair "`"))  

#+end_src

* Terminal

** VTerm

#+begin_src emacs-lisp

  (use-package vterm
    :ensure t
    :commands vterm
    :config
    (setq vterm-max-scrollback 10000
            vterm-shell "/bin/bash"))
   (use-package multi-vterm
    :ensure t
    :commands (multi-vterm multi-vterm-project))

#+end_src

* Runtime performance

#+begin_src emacs-lisp

  ;; Make gc pauses faster by decreasing the threshold.
  (setq gc-cons-threshold (* 2 1000 1000))

#+end_src
